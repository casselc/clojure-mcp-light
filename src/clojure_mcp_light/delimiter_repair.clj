(ns clojure-mcp-light.delimiter-repair
  "Delimiter error detection and repair functions using edamame and parinfer-rust"
  (:require [edamame.core :as e]
            [clojure.java.shell :as shell]
            [cheshire.core :as json]))

(def ^:dynamic *signal-on-bad-parse* true)

(defn delimiter-error?
  "Returns true if the string has a delimiter error specifically.
   Checks both that it's an :edamame/error and has delimiter info.
   Uses :all true to enable all standard Clojure reader features:
   function literals, regex, quotes, syntax-quote, deref, var, etc.
   Also enables :read-cond :allow to support reader conditionals.
   Handles unknown data readers gracefully with a default reader fn."
  [s]
  (try
    (e/parse-string-all s {:all true
                           :features #{:clj :cljs :cljr :default}
                           :read-cond :allow
                           :readers (merge *data-readers*
                                          ;; Common ClojureScript/EDN tags - treat as no-op for delimiter checking
                                           {'js (fn [x] x)
                                            'jsx (fn [x] x)
                                            'queue (fn [x] x)
                                            'date (fn [x] x)})
                           :auto-resolve name})
    false ; No error = no delimiter error
    (catch clojure.lang.ExceptionInfo ex
      (let [data (ex-data ex)]
        (and (= :edamame/error (:type data))
             (contains? data :edamame/opened-delimiter))))
    (catch Exception _
      ;; Experimentally going to return true in this case to
      ;; communication a parse failure we will run parinfer if this is
      ;; true just in case there is a delimiter error as well in the
      ;; file

      ;; running parinfer is a benign action most of the time
      *signal-on-bad-parse*)))

(defn parinfer-repair
  "Attempts to repair delimiter errors using parinfer-rust.
   Returns a map with:
   - :success - boolean indicating if repair was successful
   - :repaired-text - the repaired code (if successful)
   - :error - error message (if unsuccessful)"
  [s]
  (let [result (shell/sh "parinfer-rust"
                         "--mode" "indent"
                         "--language" "clojure"
                         "--output-format" "json"
                         :in s)
        exit-code (:exit result)]
    (if (zero? exit-code)
      (try
        (json/parse-string (:out result) true)
        (catch Exception _
          {:success false}))
      {:success false})))

(defn fix-delimiters
  "Takes a Clojure string and attempts to fix delimiter errors.
   Returns the repaired string if successful, false otherwise.
   If no delimiter errors exist, returns the original string."
  [s]
  (if (delimiter-error? s)
    (let [{:keys [text success]} (parinfer-repair s)]
      (when (and success text (not (delimiter-error? text)))
        text))
    s))
